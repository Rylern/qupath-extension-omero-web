plugins {
  id 'java-library'
  // To create a shadow/fat jar, including dependencies
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  // Reduce JavaCPP dependencies
  id 'org.bytedeco.gradle-javacpp-platform'
}

ext.moduleName = 'qupath.extension.omero'
archivesBaseName = 'qupath-extension-omero'
description = "QuPath extension to support image reading using OMERO's web API."
version = "0.4.0-SNAPSHOT"

repositories {
//  mavenLocal()
  mavenCentral()
  
  maven {
    url "https://maven.scijava.org/content/repositories/releases"
  }
  
  maven {
    url "https://maven.scijava.org/content/repositories/snapshots"
  }
  
  maven {
    name "ome.maven"
    url "https://artifacts.openmicroscopy.org/artifactory/maven"
  }

  maven {
    name "unidata.releases<"
    url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases"
  }
  
}

dependencies {
  implementation libs.commons.text

  // Auto-discover if OMERO-Gateway is available
  shadow "org.openmicroscopy:omero-gateway:5.8.0"

  shadow "io.github.qupath:qupath-gui-fx:${gradle.ext.qupathVersion}"
  shadow "org.slf4j:slf4j-api:1.7.30"
}

processResources {
  from ("${projectDir}/LICENSE.txt") {
    into 'licenses/'
  }
}

tasks.register("copyDependencies", Copy) {
  description "Copy dependencies into the output libs directory"
  group "QuPath"
  
  from configurations.default
  into 'build/libs'
}

java {
    def version = project.findProperty('toolchain')
    if (!version)
        version = 11
    else if (version.strip() == 'skip')
        version = null
    if (version != null) {
        logger.quiet("Setting toolchain to {}", version)
        toolchain {
            languageVersion = JavaLanguageVersion.of(version)
        }
    }
}
