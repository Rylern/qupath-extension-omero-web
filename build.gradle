plugins {
  id 'java-library'
  // To create a shadow/fat jar, including dependencies
  id 'com.github.johnrengelman.shadow' version '7.0.0'
    // Add JavaFX dependencies
    alias(libs.plugins.javafx)
}

ext.moduleName = 'qupath.extension.omero'
archivesBaseName = 'qupath-extension-omero'
description = "QuPath extension to support image reading using OMERO's web API."
version = "0.3.1-SNAPSHOT"
ext.qupathVersion = gradle.ext.qupathVersion
ext.qupathJavaVersion = 17

repositories {
//  mavenLocal()
  mavenCentral()
  
  maven {
    url "https://maven.scijava.org/content/repositories/releases"
  }
  
  maven {
    url "https://maven.scijava.org/content/repositories/snapshots"
  }
  
}

dependencies {
    implementation "org.apache.commons:commons-text:1.9"
    shadow "io.github.qupath:qupath-gui-fx:${qupathVersion}"
    shadow libs.qupath.fxtras

    shadow libs.slf4j
}

jar {
    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Version": archiveVersion,
                "Automatic-Module-Name": moduleName)
    }
}

processResources {
    from ("${projectDir}/LICENSE") {
        into 'licenses/'
    }
}

tasks.register("copyDependencies", Copy) {
    description "Copy dependencies into the build directory for use elsewhere"
    group "QuPath"

    from configurations.default
    into 'build/libs'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(qupathJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    def strictJavadoc = findProperty('strictJavadoc')
    if (!strictJavadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.named('compileJava') {
    options.encoding = 'UTF-8'
}

tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named('test') {
    useJUnitPlatform()
}
