plugins {
    // Main gradle plugin for building a Java library
    id 'java-library'
    // To create a shadow/fat jar that bundle up all dependencies
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    // Add JavaFX dependencies
    alias(libs.plugins.javafx)
}

ext.moduleName = 'qupath.extension.omero'
version = "0.4.0-SNAPSHOT"
description = "QuPath extension to support image reading using OMERO APIs."
ext.qupathVersion = gradle.ext.qupathVersion
ext.qupathJavaVersion = 17

dependencies {
    shadow "io.github.qupath:qupath-gui-fx:${qupathVersion}"
    shadow "io.github.qupath:qupath-extension-bioformats:${qupathVersion}"
    shadow libs.qupath.fxtras

    shadow libs.slf4j

    // Auto-discover if OMERO-Gateway is available
    shadow "org.openmicroscopy:omero-gateway:5.8.2"

    testImplementation "io.github.qupath:qupath-gui-fx:${qupathVersion}"
    testImplementation "io.github.qupath:qupath-extension-bioformats:${qupathVersion}"
    testImplementation "org.openmicroscopy:omero-gateway:5.8.2"
    testImplementation libs.junit
    testImplementation "org.testcontainers:testcontainers:1.19.1"
    testImplementation "org.testcontainers:junit-jupiter:1.19.1"
}

jar {
    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Version": archiveVersion,
                "Automatic-Module-Name": moduleName)
    }
}

processResources {
    from ("${projectDir}/LICENSE") {
        into 'licenses/'
    }
}

tasks.register("copyDependencies", Copy) {
    description "Copy dependencies into the build directory for use elsewhere"
    group "QuPath"

    from configurations.default
    into 'build/libs'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(qupathJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    def strictJavadoc = findProperty('strictJavadoc')
    if (!strictJavadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.named('compileJava') {
    options.encoding = 'UTF-8'
}

tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named('test') {
    useJUnitPlatform()
}

repositories {
    mavenCentral()

    maven {
        url "https://maven.scijava.org/content/repositories/releases"
    }

    maven {
        url "https://maven.scijava.org/content/repositories/snapshots"
    }

    maven {
        name "ome.maven"
        url "https://artifacts.openmicroscopy.org/artifactory/maven"
    }

    maven {
        name "unidata.releases<"
        url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases"
    }
}